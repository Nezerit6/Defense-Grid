apply plugin: "java"

version = '1.0'

sourceSets.main.java.srcDirs = ["src"]

repositories{
    mavenCentral()
    maven{ url = "https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository" }
    maven{ url = 'https://www.jitpack.io' }
}

java{
    targetCompatibility = 8
    sourceCompatibility = JavaVersion.VERSION_17
}

ext{
    //the build number that this mod is made for
    mindustryVersion = 'v147'
    jabelVersion = "93fde537c7"
    //windows sucks
    isWindows = System.getProperty("os.name").toLowerCase().contains("windows")
    sdkRoot = System.getenv("ANDROID_HOME") ?: System.getenv("ANDROID_SDK_ROOT")
}

//java 8 backwards compatibility flag
allprojects{
    tasks.withType(JavaCompile).tap{
        configureEach{
            options.compilerArgs.addAll(['--release', '8'])
        }
    }
}

dependencies{
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
    compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"

    annotationProcessor "com.github.Anuken:jabel:$jabelVersion"
}

//force arc version
configurations.configureEach{
    resolutionStrategy.eachDependency { details ->
        if(details.requested.group == 'com.github.Anuken.Arc'){
            details.useVersion "$mindustryVersion"
        }
    }
}

tasks.register('jarAndroid'){
    dependsOn "jar"
    doLast{
        if(!sdkRoot || !new File(sdkRoot).exists()) throw new GradleException("No valid Android SDK found. Ensure that ANDROID_HOME is set to your Android SDK directory.")
        def platformRoot = new File("$sdkRoot/platforms/").listFiles().sort().reverse().find{ f -> new File(f, "android.jar").exists() }
        if(!platformRoot) throw new GradleException("No android.jar found. Ensure that you have an Android platform installed.")

        def dependencies = (configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList() + [new File(platformRoot, "android.jar")]).collect{ "--classpath $it.path" }.join(" ")
        def d8 = isWindows ? "d8.bat" : "d8"
        def d8Path = "$sdkRoot/build-tools"
        def buildToolsDir = new File(d8Path).listFiles()?.sort()?.reverse()?.find{ it.isDirectory() }
        if(!buildToolsDir) throw new GradleException("No build-tools found in Android SDK")
        def d8Executable = new File(buildToolsDir, d8)
        if(!d8Executable.exists()) throw new GradleException("d8 not found at: $d8Executable")

        def command = "${d8Executable.absolutePath} $dependencies --min-api 14 --output ${project.name}Android.jar ${project.name}Desktop.jar"
        def process = command.execute(null, new File("build/libs"))
        process.waitForProcessOutput(System.out, System.err)
        if(process.exitValue() != 0){
            throw new GradleException("d8 failed with exit code: ${process.exitValue()}")
        }
    }
}

jar{
    archiveFileName = "${project.name}Desktop.jar"

    from{
        configurations.runtimeClasspath.collect{ it.isDirectory() ? it : zipTree(it) }
    }

    from(projectDir){
        include "mod.hjson"
    }

    from("assets/"){
        include "**"
    }
}

tasks.register('deploy', Jar){
    dependsOn jarAndroid
    dependsOn jar
    archiveFileName = "${project.name}.jar"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from(zipTree("build/libs/${project.name}Desktop.jar"))

    from(zipTree("build/libs/${project.name}Android.jar")){
        include '*.dex'
    }

    doLast{
        delete{
            delete "build/libs/${project.name}Android.jar"
            delete "build/libs/${project.name}Desktop.jar"
        }
    }
}